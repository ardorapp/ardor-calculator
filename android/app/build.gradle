def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def releaseTime() {
    return new Date().format("yyyy.MM.dd_HH.mm", TimeZone.getTimeZone("Asia/Shanghai"))
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "app.ardor.calculator"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG_APP_ARDOR_STORE_KEY_FILE)
            storePassword DEBUG_APP_ARDOR_STORE_PASSWORD
            keyAlias DEBUG_APP_ARDOR_STORE_KEY_ALIAS
            keyPassword DEBUG_APP_ARDOR_STORE_KEY_PASSWORD
        }

        release {
            storeFile file(RELEASE_APP_ARDOR_STORE_KEY_FILE)
            storePassword RELEASE_APP_ARDOR_STORE_PASSWORD
            keyAlias RELEASE_APP_ARDOR_STORE_KEY_ALIAS
            keyPassword RELEASE_APP_ARDOR_STORE_KEY_PASSWORD
        }
    }

    buildTypes {
        // TODO: Add your own signing config for the release build.
        // Signing with the debug keys for now, so `flutter run --release` works.
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            signingConfig signingConfigs.debug
        }
        release {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            signingConfig signingConfigs.release
        }
    }

    // 生成后的APK包重命名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def buildTypeName = variant.buildType.name
            if (buildTypeName == 'release') {
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    // 输出apk名称为 ArdorCalculator_v1.0.0_2019.11.27_18.13_release.apk
                    def fileName = "ArdorCalculator_v${defaultConfig.versionName}_${releaseTime()}_${buildTypeName}.apk"
                    outputFileName = fileName
                }
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
